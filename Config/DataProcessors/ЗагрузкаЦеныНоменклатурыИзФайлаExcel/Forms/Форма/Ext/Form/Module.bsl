
&НаКлиенте
Процедура ЗагрузитьПрайсЛист(Команда)
	СкопироватьФайлНаСервер("ПостроительЗапроса")
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьФайлНаСервер(СпособЗагрузки)
	
	ОповещениеОЗавершении = Новый ОписаниеОповещения("СкопироватьФайлНаСерверЗавершение", ЭтотОбъект, СпособЗагрузки);
	НачатьПомещениеФайлаНаСервер(ОповещениеОЗавершении,,,,,УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьФайлНаСерверЗавершение(ОписаниеПомещенногоФайла, ДополнительныеПараметры) Экспорт
	
	Если ОписаниеПомещенногоФайла <> Неопределено Тогда
		
		АдресФайлаВХранилище = ОписаниеПомещенногоФайла.Адрес;
		
		ЗагрузитьИзТабличногоДокументаНаСервере(АдресФайлаВХранилище, ДатаПрайсЛиста, ВидЦеныПродажи, ДополнительныеПараметры);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗагрузитьИзТабличногоДокументаНаСервере(АдресФайлаВХранилище, ДатаПрайсЛиста, ВидЦеныПродажи, ДополнительныеПараметры)
	
	ТабДок = ПрочитатьФайл(АдресФайлаВХранилище);
	ТаблицаПрайсЛиста = ЗаполнитьТаблицуЗначенийЧерезПостроительЗапроса(ТабДок);
	
	Если ТаблицаПрайсЛиста.Количество() Тогда
		ЗагрузитьПрайсЛистПоставщика(ТаблицаПрайсЛиста, ДатаПрайсЛиста, ВидЦеныПродажи);
	Иначе
		СообщениеПользователю = Новый СообщениеПользователю();
		СообщениеПользователю.Текст = "Выбранный файл не содержит строк с ценами!";
		СообщениеПользователю.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПрочитатьФайл(АдресФайлаВХранилище)
	
	ТабДок = Новый ТабличныйДокумент;
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла(".xlsx");
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
	ДвоичныеДанные.Записать(ИмяВременногоФайла);
	
	Попытка
		ТабДок.Прочитать(ИмяВременногоФайла);
	Исключение
		вызватьИсключение "Не удалось прочитать файл EXCEL в табличный документ!";
	КонецПопытки; 
	
	возврат ТабДок;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗаполнитьТаблицуЗначенийЧерезПостроительЗапроса(ТабДок)
	
	ОбластиТабличногоДокумента = ТабДок.Область(1, 1, ТабДок.ВысотаТаблицы, ТабДок.ШиринаТаблицы);
	
	Построитель = Новый ПостроительЗапроса;
	Построитель.ИсточникДанных = Новый ОписаниеИсточникаДанных(ОбластиТабличногоДокумента);
	Построитель.Выполнить();
	
	ТаблицаПрайсЛиста = Построитель.Результат.Выгрузить();
	
	возврат ТаблицаПрайсЛиста;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗагрузитьПрайсЛистПоставщика(ТаблицаПрайсЛиста, ДатаПрайсЛиста, ВидЦеныПродажи)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаПрайсЛиста", ТаблицаПрайсЛиста);
	
	Запрос.Текст=
	"ВЫБРАТЬ
	|	ТаблицаПрайсЛиста.N КАК N,
	|	ТаблицаПрайсЛиста.Номенклатура КАК Номенклатура,
	|	ТаблицаПрайсЛиста.Код КАК Код,
	|	ТаблицаПрайсЛиста.Цена КАК Цена
	|ПОМЕСТИТЬ ВТ_ТаблицаПрайсЛиста
	|ИЗ
	|	&ТаблицаПрайсЛиста КАК ТаблицаПрайсЛиста
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Ном.Ссылка КАК Номенклатура,
	|	ВТ_ТаблицаПрайсЛиста.Цена КАК Цена
	|ИЗ
	|	ВТ_ТаблицаПрайсЛиста КАК ВТ_ТаблицаПрайсЛиста
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК Ном
	|		ПО ВТ_ТаблицаПрайсЛиста.Код = Ном.Код";
	
	Рез = Запрос.Выполнить();
	Если Рез.Пустой() Тогда
		возврат;
	КонецЕсли;
	
	Выборка = Рез.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		РегистрСветденийЦеныНоменклатуры = РегистрыСведений.ЦеныНоменклатуры.СоздатьМенеджерЗаписи();
		РегистрСветденийЦеныНоменклатуры.Номенклатура = Выборка.Номенклатура;
		РегистрСветденийЦеныНоменклатуры.Период = ДатаПрайсЛиста;
		РегистрСветденийЦеныНоменклатуры.ВидЦены = ВидЦеныПродажи;
		РегистрСветденийЦеныНоменклатуры.Цена = Выборка.Цена;
		РегистрСветденийЦеныНоменклатуры.Записать();
	КонецЦикла;
	
	СообщениеПользователю = Новый СообщениеПользователю();
	СообщениеПользователю.Текст = СтрШаблон("В регистр успешно загружены цены в количестве %1 строк на %2 с видом цены %3!", Выборка.Количество(), ДатаПрайсЛиста, ВидЦеныПродажи);
	СообщениеПользователю.Сообщить();
	
Конецпроцедуры
